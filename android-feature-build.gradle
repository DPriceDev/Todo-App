apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "dev.dprice.productivity.todo.ui.test.UiTestRunner"
    }

    buildTypes {
        shared {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            initWith shared
            debuggable true
            testCoverageEnabled true
        }

        espresso {
            initWith shared
            debuggable true
            matchingFallbacks = ['debug']
        }

        release {
            shrinkResources true
            initWith shared
            minifyEnabled true
            debuggable true
        }
    }

    flavorDimensions "env"
    testBuildType "espresso"

    productFlavors {
        local {
            dimension "env"
        }

        integration {
            dimension "env"
        }

        prod {
            dimension "env"
        }
    }

    variantFilter { variant ->
        def flavorNames = variant.flavors*.name
        def buildTypeNames = variant.buildType.name

        if (variant.buildType.name in ["shared"]) {
            setIgnore(true)
        }

        if (flavorNames.contains("local") && buildTypeNames in ["release"]) {
            setIgnore(true)
        }

        if (flavorNames.contains("integration") && buildTypeNames in ["release", "espresso"]) {
            setIgnore(true)
        }
        if (flavorNames.contains("prod") && buildTypeNames in ["debug", "espresso"]) {
            setIgnore(true)
        }
    }

    sourceSets {
        integration.java.srcDirs("src/nonLocal/java")
        prod.java.srcDirs("src/nonLocal/java")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }
    kapt {
        correctErrorTypes true
    }
//    packagingOptions {
//        exclude("META-INF/AL2.0")
//        exclude("META-INF/LGPL2.1")
//    }
}

dependencies {
    implementation(projects.ui)
    implementation(projects.platform)
    implementation(projects.core)
    androidTestImplementation(projects.ui.test)

    /* Kotlin */
    implementation(libs.bundles.kotlin)

    /* Android */
    implementation(libs.bundles.android)

    /* Compose */
    implementation(libs.bundles.compose)

    implementation(libs.bundles.aws)

    /* Hilt */
    implementation(libs.bundles.hilt)
    kapt(libs.hiltCompiler)

    /* Logging */
    implementation(libs.timber)

    /* Testing */
    testImplementation(libs.bundles.unitTest)
    debugImplementation(libs.fragmentTesting)

    androidTestImplementation(libs.bundles.uiTest)

    kaptAndroidTest(libs.hiltKaptTest)
}